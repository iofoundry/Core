#Query to retrieve all Core,and non-BFO import classes
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cco: <http://www.ontologyrepository.com/CommonCoreOntologies/>
PREFIX av: <https://www.industrialontologies.org/ontology/core/meta/AnnotationVocabulary/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?class ?parentclass ?closestBFOancestor ?NLdefinition ?semiOReluc
WHERE
{
	?URI rdfs:subClassOf ?x.
  	FILTER(regex(str(?URI), "core/core|CommonCore|obo/ro", "i"))
	?x rdfs:label ?parent.
	BIND (STR(?parent)  AS ?parentclass)
	OPTIONAL {?URI rdfs:label ?class}
	OPTIONAL {?URI av:naturalLanguageDefinition ?NLdefinition}
	OPTIONAL {?URI av:semi-formalNaturalLanguageDefinition ?semiformal}
	OPTIONAL {?URI av:elucidation ?elucidation}
	OPTIONAL {?URI cco:definition ?defcco}
    OPTIONAL {?URI obo:IAO_0000115 ?obodef}
	BIND(IF(BOUND(?elucidation), ?elucidation, ?semiformal) as ?aa)  
	BIND(IF(BOUND(?aa), ?aa, ?defcco) as ?bb)  
	BIND(IF(BOUND(?bb), ?bb, ?obodef) as ?semiOReluc)  
	OPTIONAL {?URI rdfs:comment ?comment}
	OPTIONAL {?URI skos:editorialNote ?note}
	OPTIONAL {?URI skos:example ?example}
	?URI rdfs:subClassOf+ ?ancestorx.
	?ancestorx rdfs:subClassOf ?parentofancestorx.
	?parentofancestorx rdfs:label ?closestBFOancestor.
	?ancestorx rdfs:label ?ancestor.
	FILTER ((
		regex(str(?parentofancestorx), "obo/BFO", "i") &&
     	!regex(str(?ancestorx), "obo/BFO", "i")
    ) || (
    	regex(str(?ancestorx), "obo/BFO", "i") &&
        ?ancestorx = ?x 
	))
}
ORDER BY ?class
